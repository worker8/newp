apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'crashlytics'
apply plugin: 'kotlin-kapt'

ext {
    sdkVersion = 26
    minVersion = 17
    toolsVersion = '26.0.2'

    supportVersion = '26.1.0'
    constraintLayoutVersion = '1.0.2'
    archComponentVersion = '1.0.0-rc1'
    rxJavaVersion = '2.1.6'
    rxAndroidVersion = '2.0.1'
}

android {
    compileSdkVersion sdkVersion
    buildToolsVersion toolsVersion
    defaultConfig {
        applicationId "beepbeep.pixels"
        minSdkVersion minVersion
        targetSdkVersion sdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    def secretKeys = ['REDDIT_DEVELOPER_NAME', 'REDDIT_CLIENT_ID', 'REDDIT_REDIRECT_URL']
    def secretProperties = getSecretProperties(secretKeys)
    buildTypes.each { buildType ->
        secretKeys.each { key ->
            buildType.buildConfigField 'String', key, "\"${secretProperties[key]}\""
        }
    }

    buildTypes {
        debug {

        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    repositories {
        google()
        jcenter()
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile project(':rxjraw')
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    compile "android.arch.lifecycle:runtime:$archComponentVersion"
    compile "android.arch.lifecycle:extensions:$archComponentVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$archComponentVersion"
    compile "android.arch.persistence.room:rxjava2:$archComponentVersion"
    kapt "android.arch.persistence.room:compiler:$archComponentVersion"

    compile "android.arch.persistence.room:runtime:$archComponentVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$archComponentVersion"

    compile "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    compile "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    compile "com.jakewharton.rxbinding2:rxbinding:2.0.0"

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"

    debugCompile 'com.amitshekhar.android:debug-db:1.0.1'

    testCompile 'junit:junit:4.12'
    testCompile('com.nhaarman:mockito-kotlin:1.5.0') {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    }
    // testCompile "android.arch.persistence.room:testing:$archComponentVersion"
}

def getSecretProperties(ArrayList<String> keys) {
    def properties = new Properties()
    if (System.getenv('CI')) {
        keys.each { properties.setProperty(it, System.getenv(it)) }
    } else {
        properties.load(new FileInputStream(rootProject.file("secrets.properties")))
    }
    return properties
}